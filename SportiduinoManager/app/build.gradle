import com.github.spotbugs.SpotBugsTask
plugins {
    id "com.github.spotbugs" version "2.0.0"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'com.github.spotbugs'

android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "ru.mmb.sportiduinomanager"
        minSdkVersion 14
        targetSdkVersion 28
        resConfigs "ru", "en"
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

checkstyle {
    toolVersion "8.25"
}
task checkstyle(type: Checkstyle) {
    group 'verification'
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    classpath = files()
}

pmd {
    toolVersion "6.18.0"
}
task pmd(type: Pmd) {
    group 'verification'
    ruleSets = []
    ruleSetFiles = files("$project.rootDir/config/pmd/rules-pmd.xml")
    source 'src'
    include '**/*.java'
}

spotbugs {
    toolVersion = '4.0.0-beta4'
}
tasks.withType(SpotBugsTask) {
    group = "verification"
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    classes = files("build/intermediates/javac/debug")
    source 'src'
    include '**/*.java'
    classpath = files()
    reports {
        xml.enabled = true
        html.enabled = false
    }
}
sourceSets {
     // we define `main` sourceSet here, so SpotBugs Gradle Plugin generates `spotbugsMain` task
     main {
       java.srcDirs = ['src/main/java']
     }
}

dependencies {
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:design:28.0.0"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.13'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    final def acraVersion = '5.4.0'
    implementation "ch.acra:acra-core:$acraVersion"
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-toast:$acraVersion"
}
repositories {
    mavenCentral()
}