plugins {
    id 'com.github.spotbugs' version '1.6.10'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

android {

    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "ru.mmb.sportiduinomanager"
        minSdkVersion 19
        targetSdkVersion 28
        resConfigs "ru", "en"
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}


apply plugin: 'checkstyle'
checkstyle {
    toolVersion "8.14"
}
task checkstyle(type: Checkstyle) {
    group 'verification'
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    classpath = files()
}

apply plugin: 'findbugs'
task findbugs(type: FindBugs) {
    group 'verification'
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    classes = files("build/intermediates/javac/debug")
    source 'src'
    include '**/*.java'
    classpath = files()
}

apply plugin: 'pmd'
pmd {
    toolVersion "6.11.0"
}
task pmd(type: Pmd) {
    group 'verification'
    ruleSets = []
    ruleSetFiles = files("$project.rootDir/config/pmd/rules-pmd.xml")
    source 'src'
    include '**/*.java'
}

spotbugs {
    toolVersion = '3.1.11'
}
tasks.withType(com.github.spotbugs.SpotBugsTask) {
    group = "verification"
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    classes = files("build/intermediates/javac/debug")
    source 'src'
    include '**/*.java'
    classpath = files()
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
sourceSets {
     // we define `main` sourceSet here, so SpotBugs Gradle Plugin generates `spotbugsMain` task
     main {
       java.srcDirs = ['src/main/java']
     }
}

dependencies {
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:design:28.0.0"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    final def acraVersion = '5.3.0'
    implementation "ch.acra:acra-core:$acraVersion"
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-toast:$acraVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}