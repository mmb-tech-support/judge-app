import com.github.spotbugs.snom.SpotBugsTask
plugins {
    id "com.github.spotbugs" version "4.5.1"
}
apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'com.github.spotbugs'

android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile file(SPORTIDUINO_STORE_FILE)
            storePassword SPORTIDUINO_STORE_PASSWORD
            keyAlias SPORTIDUINO_KEY_ALIAS
            keyPassword SPORTIDUINO_KEY_PASSWORD
        }
    }

    defaultConfig {
        applicationId "ru.mmb.sportiduinomanager"
        minSdkVersion 14
        targetSdkVersion 28
        resConfigs "ru", "en"
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

checkstyle {
    toolVersion "8.36.2"
}
task checkstyle(type: Checkstyle) {
    group 'verification'
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    classpath = files()
}

pmd {
    toolVersion "6.28.0"
}
task pmd(type: Pmd) {
    group 'verification'
    ruleSets = []
    ruleSetFiles = files("$project.rootDir/config/pmd/rules-pmd.xml")
    source 'src'
    include '**/*.java'
}

tasks.withType(SpotBugsTask) {
    group = "verification"
    //noinspection GrFinalVariableAccess
    excludeFilter = file("${project.rootDir}/config/spotbugs/spotbugs-exclude.xml")
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:design:28.0.0"
    implementation 'com.android.support.constraint:constraint-layout:2.0.2'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'

    final def acraVersion = '5.4.0'
    implementation "ch.acra:acra-core:$acraVersion"
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-toast:$acraVersion"
}
repositories {
    mavenCentral()
}